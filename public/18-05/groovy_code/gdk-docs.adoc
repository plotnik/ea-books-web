= The Groovy Development Kit
:toc: right
:toclevels: 6
:icons: font
:source-highlighter: pygments
:source-language: groovy
:pygments-style: xcode

http://groovy-lang.org/groovy-dev-kit.html

== 1. Working with IO

=== 1.3. Traversing file trees

```
dir.eachFile { file ->                      
    println file.name
}
dir.eachFileMatch(~/.*\.txt/) { file ->     
    println file.name
}

dir.eachFileRecurse { file ->                      
    println file.name
}
dir.eachFileRecurse(FileType.FILES) { file ->      
    println file.name
}

```

For more complex traversal techniques you can use the traverse method, 
which requires you to set a special flag indicating what to do with the traversal:

```
dir.traverse { file ->
    if (file.directory && file.name=='bin') {
        FileVisitResult.TERMINATE                   
    } else {
        println file.name
        FileVisitResult.CONTINUE                    
    }
}

```

== 2. Working with collections

[[manipulating_lists]]
=== 2.1.4. Manipulating lists

http://groovy-lang.org/groovy-dev-kit.html#_manipulating_lists

==== Filtering and searching

The link:http://www.groovy-lang.org/gdk.html[Groovy development kit] contains a lot of methods on collections 
that enhance the standard collections with pragmatic methods, some of which are illustrated here:

```
assert [1, 2, 3].find { it > 1 } == 2           // find 1st element matching criteria
assert [1, 2, 3].findAll { it > 1 } == [2, 3]   // find all elements matching critieria

// find index of 1st element matching criteria
assert ['a', 'b', 'c', 'd', 'e'].findIndexOf {  
    it in ['c', 'e', 'g']
} == 2

assert ['a', 'b', 'c', 'd', 'c'].indexOf('c') == 2  // index returned
assert ['a', 'b', 'c', 'd', 'c'].indexOf('z') == -1 // index -1 means value not in list
assert ['a', 'b', 'c', 'd', 'c'].lastIndexOf('c') == 4

assert [1, 2, 3].every { it < 5 }   // returns true if all elements match the predicate
assert ![1, 2, 3].every { it < 3 }
assert [1, 2, 3].any { it > 2 }     // returns true if any element matches the predicate
assert ![1, 2, 3].any { it > 3 }

assert [1, 2, 3, 4, 5, 6].sum() == 21  // sum anything with a plus() method
assert ['a', 'b', 'c', 'd', 'e'].sum {
    it == 'a' ? 1 : it == 'b' ? 2 : it == 'c' ? 3 : it == 'd' ? 4 : it == 'e' ? 5 : 0
    // custom value to use in sum
} == 15
assert ['a', 'b', 'c', 'd', 'e'].sum { ((char) it) - ((char) 'a') } == 10
assert ['a', 'b', 'c', 'd', 'e'].sum() == 'abcde'
assert [['a', 'b'], ['c', 'd']].sum() == ['a', 'b', 'c', 'd']

// an initial value can be provided
assert [].sum(1000) == 1000
assert [1, 2, 3].sum(1000) == 1006

assert [1, 2, 3].join('-') == '1-2-3'           // String joining
assert [1, 2, 3].inject('counting: ') {
    str, item -> str + item                     // reduce operation
} == 'counting: 123'
assert [1, 2, 3].inject(0) { count, item ->
    count + item
} == 6
```